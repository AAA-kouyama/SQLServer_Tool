-- =============================================
-- Author:      AAA_kouyama
-- Create date: 2024/05/24
-- Description: 使用されているオブジェクトの依存先を一括で取得ツール
-- =============================================
CREATE PROCEDURE [dbo].[Tool_ReferencedObject_list]
AS
BEGIN
    BEGIN TRY
        -- データベース名を格納する変数を作成
        DECLARE @DBName NVARCHAR(128)
        DECLARE @SQL NVARCHAR(MAX)

        -- データベース名を取得するカーソルを作成
        DECLARE db_cursor CURSOR FOR
        SELECT name FROM master.sys.databases
        WHERE name NOT IN ('master', 'tempdb', 'model', 'msdb') -- システムデータベースを除外
        AND state_desc = 'ONLINE' -- オフラインのデータベースを除外
        OPEN db_cursor
        FETCH NEXT FROM db_cursor INTO @DBName

        WHILE @@FETCH_STATUS = 0
        BEGIN
            SET @SQL = 'USE [' + @DBName + ']; ' +
                        'SELECT ' + 
                        '''' + @DBName + ''' as 参照元_database, ' +
                        'OBJECT_NAME(referencing_id) AS 参照元_object, ' +
                        'CASE WHEN OBJECT_NAME(referenced_id) IS NULL THEN referenced_entity_name ' +
                        '     ELSE  OBJECT_NAME(referenced_id) ' +
                        'END as 参照先_object, ' +
                        'CASE WHEN referenced_server_name IS NULL THEN @@SERVERNAME ' +
                        '     ELSE referenced_server_name ' +
                        'END as 参照先_server_name, ' +
                        'CASE WHEN referenced_database_name IS NULL THEN DB_NAME() ' +
                        '     ELSE referenced_database_name ' +
                        'END as 参照先_database_name, ' +
                        'CASE WHEN referenced_schema_name IS NULL THEN SCHEMA_NAME() ' +
                        '     ELSE referenced_schema_name ' +
                        'END as 参照先_schema_name ' +
                        'FROM sys.sql_expression_dependencies '

            EXEC sp_executesql @SQL

            FETCH NEXT FROM db_cursor INTO @DBName
        END

        CLOSE db_cursor
        DEALLOCATE db_cursor
    END TRY
    BEGIN CATCH
		DECLARE @ErrorMessage NVARCHAR(MAX), @ErrorSubject NVARCHAR(100)
		SELECT 
			@ErrorMessage = ERROR_MESSAGE(),
			@ErrorSubject = 'エラーが発生しました: ' + ERROR_PROCEDURE()

		EXEC msdb.dbo.sp_send_dbmail
			@profile_name = 'AAA',
			@recipients = 'kouyama-tsuy@aeonpeople.biz',
			@body = @ErrorMessage,
			@subject = @ErrorSubject

			RAISERROR('失敗しました。',1,1)
			RETURN -1
    END CATCH
END
